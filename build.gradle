buildscript {
    repositories {
        mavenCentral()

        maven {
            name 'forge'
            url 'https://maven.minecraftforge.net'
        }
        maven {
            // GTNH ForgeGradle Fork
            name = "GTNH Maven"
            url = "http://jenkins.usrv.eu:8081/nexus/content/groups/public/"
        }
        maven {
            name 'sonatype'
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            name 'Scala CI dependencies'
            url 'https://repo1.maven.org/maven2/'
        }
        gradlePluginPortal()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2.11'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        classpath "se.bjurr.gitchangelog:git-changelog-gradle-plugin:1.65"
    }
}

apply plugin: 'forge'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'se.bjurr.gitchangelog.git-changelog-gradle-plugin'

version = new File("version.txt").getText("UTF-8").trim()
group= "net.glease"
archivesBaseName = "Thaumcraft4Tweaks"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    replaceIn "TC4Tweak.java"
	replace '${version}', project.version
    runDir = "run"
}

repositories {
    maven {
        name 'gt'
        url 'http://gregtech.overminddl1.com/'
    }
    maven {
        name 'GTNH Maven'
        url 'http://jenkins.usrv.eu:8081/nexus/content/groups/public/'
    }
    maven {
        name 'minetweaker'
        url 'https://maven.blamejared.com/'
    }
}

dependencies {
    compile "com.azanor.baubles:Baubles:1.7.10-1.0.1.10:deobf"
    compile "thaumcraft:Thaumcraft:1.7.10-4.2.3.5:dev"
    compileOnly "MineTweaker3:MineTweaker3-API:3.0.11.23"
    compile "com.github.GTNewHorizons:NotEnoughItems:2.3.54-GTNH:dev"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes "FMLCorePlugin": "net.glease.tc4tweak.asm.LoadingPlugin"
        attributes "FMLCorePluginContainsFMLMod": true
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
    manifest {
        attributes "FMLCorePlugin": "net.glease.tc4tweak.asm.LoadingPlugin"
        attributes "FMLCorePluginContainsFMLMod": true
    }
}

task devJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
    manifest {
        attributes "FMLCorePlugin": "net.glease.tc4tweak.asm.LoadingPlugin"
        attributes "FMLCorePluginContainsFMLMod": true
    }
}

artifacts {
    archives(devJar)
    archives(sourceJar)
}

curseforge {
    apiKey = project.hasProperty('curseforgeAPI') ? project.curseforgeAPI : ''
    project {
        id = '431297'
        changelog = new File("CHANGELOG.md")
        releaseType = version.contains('beta') ? 'beta' : version.contains('alpha') ? 'alpha' : 'release'
        changelogType = 'markdown'
        relations {
            requiredDependency 'thaumcraft'
        }
        addGameVersion '1.7.10'

        mainArtifact(jar)
        addArtifact(devJar)
        addArtifact(sourceJar)
    }
}

task createSignedJarDir() {
    onlyIf { // Skip the task if our secret data isn't available
        project.hasProperty('keyStore')
    }
    doLast {
        mkdir new File(jar.destinationDir, "signed").toString()
    }
}

task generateGitChangelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
    file = new File("CHANGELOG.md")
    toRef = "HEAD"
    templateContent = """<!-- Generated file. DO NOT HAND EDIT!!! -->
# Change log
Change log generated from git history.
{{#tags}}
## {{name}}
 {{#issues}}
  {{#commits}}
   1. {{{messageTitle}}} ([@{{{hash}}} by {{{authorName}}}](https://github.com/Glease/TC4Tweaks/commit/{{{hashFull}}}))
  {{/commits}}
 {{/issues}}
{{/tags}}
"""
}

// sign jars
task signJar() {
    dependsOn reobf
    dependsOn createSignedJarDir
    onlyIf { // Skip the task if our secret data isn't available
        project.hasProperty('keyStore')
    }
    inputs.files jar.outputs.files
    jar.outputs.files.forEach {
        outputs.file new File(new File(jar.destinationDir, "signed"), it.name)
    }
    doLast {
        jar.outputs.files.forEach {
            ant.signjar destDir: new File(jar.destinationDir, "signed"),
                    sigfile: project.hasProperty("signerName") ? project.signerName : project.keyStoreAlias,
                    keystore: project.keyStore,
                    alias: project.keyStoreAlias,
                    storepass: project.keyStorePass,
                    keypass: project.keyStoreKeyPass,
                    jar: it
        }
    }
}

assemble.dependsOn signJar
tasks.whenTaskAdded { it->
    if (it.name.startsWith('curseforge')) {
        it.dependsOn generateGitChangelog
        it.onlyIf {
            project.hasProperty('curseforgeAPI')
        }
    }
}

install.dependsOn reobf

idea {
    module {
        inheritOutputDirs = false
        outputDir = new File(compileJava.destinationDir.parentFile.parentFile, "idea")
        testOutputDir = new File(compileTestJava.destinationDir.parentFile.parentFile, "ideaTest")
    }
}
